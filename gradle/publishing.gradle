// not in kotlin script because of issues with the bintray plugin
apply plugin: "com.jfrog.bintray"
apply plugin: "maven"
apply plugin: "maven-publish"


def artifactName() {
    project.ext.get("publishing.artifact")
}

def description() {
    project.ext.get("publishing.description")
}

def webUrl = "https://github.com/platoblm/replay-proxy"

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}


// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "Unlicense"
            url "http://unlicense.org"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "platoblm"
            name "Plato B"
            email "plato.blm@gmail.com"
        }
    }

    scm {
        url webUrl
    }
}

// Create the publication with the pom configuration:
publishing {
    publications {
        CommonPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId = 'io.deloop.tools'
            artifactId = artifactName()
            version = project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', description())
                root.appendNode('name', 'Replay Proxy')
                root.appendNode('url', webUrl)
                root.children().last() + pomConfig
            }
        }
    }
}


bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['CommonPublication']
    publish = true
    dryRun = false

    pkg {
        repo = 'tools'
        userOrg = 'deloop'
        name = artifactName()
        desc = description()
        websiteUrl = webUrl
        vcsUrl = 'https://github.com/platoblm/replay-proxy.git'
        licenses = ['Unlicense']
        publicDownloadNumbers = true
        version {
            released = new Date()
            name = project.version
            vcsTag = project.version
        }
    }
}


